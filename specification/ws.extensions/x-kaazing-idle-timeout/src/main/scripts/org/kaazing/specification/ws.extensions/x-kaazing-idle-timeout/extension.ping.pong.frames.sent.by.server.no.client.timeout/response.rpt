accept http://localhost:8001/echo
accepted
connected

read method "GET"
read version "HTTP/1.1"
read header "Host" "localhost:8001"
read header "Upgrade" /(?i:websocket)/
read header "Connection" /(?i:Upgrade)/
read header "Sec-WebSocket-Key" /(?<key>[a-zA-Z0-9+\/=]{24})/
read header "Sec-WebSocket-Version" "13"
read header "Sec-WebSocket-Extensions" "x-kaazing-idle-timeout,x-kaazing-ping-pong"

write status "101" "Switching Protocols"
write version "HTTP/1.1"
write header "Upgrade" "websocket"
write header "Connection" "Upgrade"
write header "Sec-WebSocket-Accept" ${ws:handshakeHash(key)}
write header "Sec-WebSocket-Extensions" "x-kaazing-idle-timeout; timeout=2000"
write header "Sec-WebSocket-Extensions" "x-kaazing-ping-pong; 01010102"

write notify HANDSHAKE_COMPLETE

################################################################
# Write first PING frame @ TICK_ONE within the timeout frequency
write await TICK_ONE

# Send extended PING
write [0x81 0x06 0x01 0x01 0x01 0x02 0x09 0x00]

# Expect (masked) extended PONG response (unmasked form is [0x81 0x06 0x01 0x01 0x01 0x02 0x0a 0x00])
read [0x81 0x86] ([0..4] :readMask)
read option mask ${readMask}
read [0x01 0x01 0x01 0x02 0x0a 0x00]
read option mask [0x00 0x00 0x00 0x00]
################################################################
# Write second PING frame @ TICK_TWO within the timeout frequency
write await TICK_TWO

# Send extended PING
write [0x81 0x06 0x01 0x01 0x01 0x02 0x09 0x00]

# Expect (masked) extended PONG response (unmasked form is [0x81 0x06 0x01 0x01 0x01 0x02 0x0a 0x00])
read [0x81 0x86] ([0..4] :readMask)
read option mask ${readMask}
read [0x01 0x01 0x01 0x02 0x0a 0x00]
read option mask [0x00 0x00 0x00 0x00]
################################################################

# Close initiated by client
read [0x88 0x80] [0..4]
write [0x88 0x00]

close
closed
