accept http://localhost:8001/echo
accepted
connected

read method "GET"
read version "HTTP/1.1"
read header "Host" "localhost:8001"
read header "Upgrade" /(?i:websocket)/
read header "Connection" /(?i:Upgrade)/
read header "Sec-WebSocket-Key" /(?<key>[a-zA-Z0-9+\/=]{24})/
read header "Sec-WebSocket-Version" "13"
read header "Sec-WebSocket-Extensions" "x-kaazing-idle-timeout"

write status "101" "Switching Protocols"
write version "HTTP/1.1"
write header "Upgrade" "websocket"
write header "Connection" "Upgrade"
write header "Sec-WebSocket-Accept" ${ws:handshakeHash(key)}
write header "Sec-WebSocket-Extensions" "x-kaazing-idle-timeout; timeout=2000"

write notify HANDSHAKE_COMPLETE

################################################################
# Write first PING frame @ TICK_ONE within the timeout frequency
write await TICK_ONE

# Send PING
write [0x89 0x00]

# Read PONG response
read [0x8a 0x80] [0..4]
################################################################
# Write second PING frame @ TICK_TWO within the timeout frequency
write await TICK_TWO

# Send PING
write [0x89 0x00]

# Read PONG response
read [0x8a 0x80] [0..4]
################################################################

# Do clean close
read [0x88 0x80] [0..4]
write [0x88 0x00]

close
closed
